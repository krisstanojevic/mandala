dim 90 90
background 0 0 0 

%centar slike
point O 45 45 
 
point C 32 45 
circle cprime O C

%tacka sa glavnog kruga (na sredini x ose platna na kome crtamo) u odnosu na koju radimo rotaciju da napravimo iste sare kako bi mandala bila simetricna
point R 45 58 
getx x R
gety y R

expression i {0}
expression j {0.2}
expression l {0.2}

%12 zato sto nam je potrebno da se toliko puta sara kopira da bi obisla ceo krug
while {i < 12}
{
   expression n {i*30} 
   %OY su centai zutih krugova
   rotate OY O n R
   %OW su centri belih krugova neobojenih 
   towards OW OY O 0.3  
   %OY1 su tacke na ivicama zutih krugova
   towards OY1 OY OW 0.4

   circle cyellow OY OY1
   color 255 255 102
   drawcircle cyellow 

   %rotacija centralnog kruga za rotaciju centralnog dela crteza
   point Ugao 0 0 360 0
   getx angl Ugao
   rotate OY1 O angl OY1
   %drawarc_p OY O angl 
   %drawcircle OY OY1
   
   fillcircle cyellow
   color 255 255 255
   circle cwhite OW OY1
   drawcircle cwhite

   drawsegment OY1 O   
   %petouglovi koji prave ovu saru ispod zutih krugova
   color 51 51 255  
   drawpolygon O OY1 5

   %ovo su linije koje krecu od zutih krugova prvo kao trouglovi a onda svakim korakom se povecava stepen sve do 30-ougla
   point D 3 3 30 30
   getx dx D
   drawpolygon OY1 O dx

   drawpolygon O OY1 3
   
   %centar, tamno crveni trouglovi
   while {l < 0.8}
   {
     towards OT OY1 O l
     expression l {l + 0.2}
     color 255 0 0
     drawpolygon O OT 3 
   }

   %ovo je centralni setlo narandjasti deo
   towards ON OY1 O 0.7
   color 255 178 102
   rotate G O 30 ON  
   point H 45 0 43 0 
   getx xh H
   gety yg G 
   point G1 xh yg 
   drawpolygon O G1 3
            

   getx x OY
   gety y OY
   expression j {j + 0.2}
   expression i {i + 1}
}



%periferni crveni krugovi
expression i {0}
while {i < 360}
{  
   expression i1 {i + 5}
   color 255 i i1
   %V je centar prvog okvirnog kruga, skroz gore; od njega pravimo centre rotacijom za ostale okvirne crvene krugove - V1
   point V 45 82         
   %F je tacka sa prvog crveni krug
   point F 39 82 
   %pravimo centre i tacke sa kruga za ostale crvene krugove
   rotate V1 O i V
   rotate F1 O i F
   circle cred V1 F1
   drawcircle cred
   
   %pravimo krugove unutar crvenih krugova
   expression j {30}
   while {j < 360}
   {
      %centri unutrasnjih crvenih krugova su malo pomereni, pravimo ih u odnosu na F2 i oni ce biti FC:
      towards F2 F1 V1 0.65 
      %FC centri unutrasnjih krugova
      rotate FC V1 j F2   
      %FI su tacke koje ce biti na unutrasnjim krugovima
      rotate FI V1 j F1    

      %rotacija unutrasnjih crvenih krugova
      point Ugao 360 0 0 0
      getx angl Ugao
      rotate FI V1 angl  FI
      rotate FC V1 angl FC
      drawcircle FC FI    

      expression j {j + 30}
   }
   
   expression i {i + 30}
}



animation_frames 100 10

















